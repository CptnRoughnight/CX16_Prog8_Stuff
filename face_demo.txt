100 REM LOAD BINARY FILES
110 ADDR=$4000
120 VLOAD "FACE.BIN",8,0,ADDR
130 VLOAD "FACE-PALETTE.BIN",8,1,$FA20:REM TO PALETTE OFFSET 1
140 REM
150 REM SETUP SCREEN
160 SCREEN 3:REM SET SCREEN TO 320X240
170 POKE $9F29,PEEK($9F29) OR %01000000:REM ENABLE SPRITES
180 REM
190 REM SET UP SPRITES
200 REG=$FC00
210 X=0:Y=0
220 FOR I=0 TO 0:REM LOOP THROUGH ALL SPRITES
230 VPOKE 1,REG,ADDR/32 AND 255:REM ADDRESS BITS 12:5
240 VPOKE 1,REG+1,0+ADDR/8192:REM COLOR MODE + ADDRESS BITS 16:13
250 VPOKE 1,REG+2,X AND 255
260 VPOKE 1,REG+3,X/256
270 VPOKE 1,REG+4,Y AND 255
280 VPOKE 1,REG+5,Y/256
290 VPOKE 1,REG+6,%00001100:REM SET Z-DEPTH
300 VPOKE 1,REG+7,%01010001:REM HEIGHT=16, WIDTH=16, PALETTE OFFSET=1
310 REG=REG+8
320 ADDR=ADDR+128:REM ADD SIZE OF SPRITE IN BYTES
330 X=X+16
340 IF X=320 THEN Y=Y+16
350 IF X=320 THEN X=0
360 IF Y>=240-16 THEN 380
370 NEXT
380 GET A$:IF A$="" THEN 380
390 REM
400 REM RESTORE SCREEN
410 POKE $9F29,PEEK($9F29) AND %10111111:REM DISABLE SPRITES
420 SCREEN 0:REM SET SCREEN TO 640X480
430 REM RESTORE ORIGINAL CHAR SET THAT MIGHT HAVE BEEN OVERWRITTEN
440 PRINT CHR$($8E):REM SET UPPER CASE (CAUSES UPLOAD OF CHAR SET)
